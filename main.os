Перем АдресАпи;
Перем АдресДеталейЗадач;
Перем АдресКомментариевЗадачи;

Процедура ОбработатьПомещениеИзменений(Контекст) Экспорт
	ВыполнитьОбработкуСобытия(Контекст);
КонецПроцедуры

Процедура ОбработатьИзменениеВерсии(Контекст) Экспорт
	Контекст.ВызватьИсключение("Изменение версии задним числом запрещено");
КонецПроцедуры

Процедура ВыполнитьОбработкуСобытия(Контекст)

	Попытка

		Параметры = ВыполнитьВалидациюПараметровСкрипта(Контекст);

		ПроверитьНаличиеЗадачи(Контекст, Параметры);

		Контекст.ПередатьЗапрос();

	Исключение

		Контекст.ВызватьИсключение(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));

	КонецПопытки;

КонецПроцедуры

Процедура ПроверитьНаличиеЗадачи(Контекст, Параметры)

	КлючЗадачи = ИзвлечьКлючЗадачиИзКомментария(Контекст.Комментарий, Параметры.ВыражениеПоискаКлючаЗадачи);

	Соединение = ПолучитьСоединение();
	
	Запрос = ПодготовитьЗапросЗадачи(КлючЗадачи, Параметры.ТокенДоступа, Параметры.ИдентификаторОрганизации);
	Ответ = Соединение.Получить(Запрос);

	ОбработатьОтветЗапросаЗадачи(Ответ, КлючЗадачи);

	Если Параметры.ФиксироватьИзмененияВКомментарииКЗадаче Тогда
		ЗаписатьФактПомещенияИзмененийВЗадачу(
			Контекст, Соединение, КлючЗадачи, Параметры.ТокенДоступа, Параметры.ИдентификаторОрганизации);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаписатьФактПомещенияИзмененийВЗадачу(Контекст, Соединение, КлючЗадачи, Токен, Организация)

	Комментарий = 
		"В хранилище зафиксированы изменения по задаче. Комментарий разработчика:" +
		Символы.ПС +
		СокрЛП(СтрЗаменить(Контекст.Комментарий, КлючЗадачи, ""));

	Запрос = ПодготовитьЗапросДобавленияКомментарияЗадачи(КлючЗадачи, Токен, Организация, Комментарий);
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	ОбработатьОтветДобавленияКомментарияКЗадаче(Ответ);

КонецПроцедуры

Функция ДесериализоватьТелоОтвета(ТелоОтвета)

	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(ТелоОтвета);

	ДанныеОтвета = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();

	Возврат ДанныеОтвета;

КонецФункции

Процедура ОбработатьОтветДобавленияКомментарияКЗадаче(Ответ)

	Если Ответ.КодСостояния <> 201 Тогда
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
		ВызватьИсключение "Ошибка добавления комментария к задаче:" + Символы.ПС + ТелоОтвета;
	КонецЕсли

КонецПроцедуры

Процедура ОбработатьОтветЗапросаЗадачи(Ответ, КлючЗадачи)

	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();
	ДанныеОтвета = ДесериализоватьТелоОтвета(ТелоОтвета);

	Если Ответ.КодСостояния = 200 Тогда
		ВыполнитьПроверкуСостоянияЗадачи(ДанныеОтвета);
	Иначе
		Если Ответ.КодСостояния = 404 Тогда
			ВызватьИсключение "Задача с идентификатором " + КлючЗадачи + " не найдена";
		Иначе
			ВызватьИсключение ТелоОтвета;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьПроверкуСостоянияЗадачи(Задача)

	Если Задача.status.key <> "open" Тогда
		ВызватьИсключение "Фиксация изменений для задачи с состоянием """ + Задача.status.display + """ запрещена";
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьСоединение()

	Таймаут = 10;
	Возврат Новый HTTPСоединение(АдресАпи,,,,, Таймаут);

КонецФункции

Функция ПодготовитьЗаголовки(Токен, Идентификатор)

	Заголовки = Новый Соответствие();

	Заголовки.Вставить("HOST", "api.tracker.yandex.net");
	Заголовки.Вставить("Authorization", "OAuth " + Токен);
	Заголовки.Вставить("X-Cloud-Org-ID", Идентификатор);
	Заголовки.Вставить("Accept-Language", "ru");

	Возврат Заголовки;

КонецФункции

Функция ПодготовитьЗапросЗадачи(КлючЗадачи, Токен, Идентификатор)

	Заголовки = ПодготовитьЗаголовки(Токен, Идентификатор);

	Запрос = Новый HTTPЗапрос(АдресДеталейЗадач + "/" + КлючЗадачи, Заголовки);

	Возврат Запрос;

КонецФункции

Функция ПодготовитьЗапросДобавленияКомментарияЗадачи(КлючЗадачи, Токен, Идентификатор, Комментарий)

	Запрос = ПодготовитьЗапросКомментариевЗадачи(КлючЗадачи, Токен, Идентификатор);
	
	Запись = Новый ЗаписьJSON();
	Запись.УстановитьСтроку();

	ДанныеЗапроса = Новый Структура;
	ДанныеЗапроса.Вставить("text", Комментарий);

	ЗаписатьJSON(Запись, ДанныеЗапроса);

	ДанныеСтрока = Запись.Закрыть();
	Запрос.УстановитьТелоИзСтроки(ДанныеСтрока);

	Возврат Запрос;

КонецФункции

Функция ПодготовитьЗапросКомментариевЗадачи(КлючЗадачи, Токен, Идентификатор)

	Заголовки = ПодготовитьЗаголовки(Токен, Идентификатор);

	РеальныйАдрес = СтрЗаменить(АдресКомментариевЗадачи, "{КлючЗадачи}", КлючЗадачи);

	Запрос = Новый HTTPЗапрос(РеальныйАдрес, Заголовки);

	Возврат Запрос;

КонецФункции

Функция ИзвлечьКлючЗадачиИзКомментария(Комментарий, РегулярноеВыражение)

	Выражение = Новый РегулярноеВыражение(РегулярноеВыражение);
	Выражение.ИгнорироватьРегистр = Истина;

	Совпадения = Выражение.НайтиСовпадения(Комментарий);

	Если Совпадения.Количество() <> 1 Тогда
		ВызватьИсключение "Не удалось извлечь ключ задачи из комментария";
	КонецЕсли;

	Возврат Совпадения[0].Значение;

КонецФункции

Функция ВыполнитьВалидациюПараметровСкрипта(Контекст)

	ТокенДоступа = Контекст.ДополнительныеПараметры.Получить("ТокенДоступа");
	ИдентификаторОрганизации = Контекст.ДополнительныеПараметры.Получить("ИдентификаторОрганизации");
	ВыражениеПоискаКлючаЗадачи = Контекст.ДополнительныеПараметры.Получить("ВыражениеПоискаКлючаЗадачи");
	ФиксироватьИзмененияВКомментарииКЗадаче = 
		Контекст.ДополнительныеПараметры.Получить("ФиксироватьИзмененияВКомментарииКЗадаче");
	
	Параметры = Новый Структура();
	Параметры.Вставить(
		"ФиксироватьИзмененияВКомментарииКЗадаче", 
		ВРег(ФиксироватьИзмененияВКомментарииКЗадаче) = "ИСТИНА");
	
	Если ТокенДоступа = Неопределено Тогда
		ВызватьИсключение "Токен доступа к API яндекс трекера не указан";
	КонецЕсли;

	Если ИдентификаторОрганизации = Неопределено Тогда
		ВызватьИсключение "Идентификатор организации не указан";
	КонецЕсли;

	Если ВыражениеПоискаКлючаЗадачи = Неопределено Тогда
		ВызватьИсключение "Выражение поиска ключа задачи не указано";
	КонецЕсли;

	Параметры.Вставить("ТокенДоступа", ТокенДоступа);
	Параметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	Параметры.Вставить("ВыражениеПоискаКлючаЗадачи", ВыражениеПоискаКлючаЗадачи);

	Возврат Параметры;

КонецФункции

АдресАпи = "https://api.tracker.yandex.net";
АдресДеталейЗадач = "/v3/issues";
АдресКомментариевЗадачи = "/v3/issues/{КлючЗадачи}/comments";