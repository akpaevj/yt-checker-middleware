Перем АдресАпи;
Перем АдресДеталейЗадач;

Процедура ОбработатьПомещениеИзменений(Контекст) Экспорт
	ВыполнитьОбработкуСобытия(Контекст);
КонецПроцедуры

Процедура ОбработатьИзменениеВерсии(Контекст) Экспорт
	ВыполнитьОбработкуСобытия(Контекст);
КонецПроцедуры

Процедура ВыполнитьОбработкуСобытия(Контекст)

	Попытка

		Параметры = ВыполнитьВалидациюПараметровСкрипта(Контекст);

		ПроверитьНаличиеЗадачи(Контекст, Параметры);

		Контекст.ПередатьЗапрос();

	Исключение

		Контекст.ВызватьИсключение(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));

	КонецПопытки;

КонецПроцедуры

Процедура ПроверитьНаличиеЗадачи(Контекст, Параметры)

	КлючЗадачи = ИзвлечьКлючЗадачиИзКомментария(Контекст.Комментарий, Параметры.ВыражениеПоискаКлючаЗадачи);

	Соединение = ПолучитьСоединение();
	
	Запрос = ПодготовитьЗапрос(АдресДеталейЗадач, КлючЗадачи, Параметры.ТокенДоступа, Параметры.ИдентификаторОрганизации);
	Ответ = Соединение.Получить(Запрос);

	ОбработатьОтвет(Ответ, КлючЗадачи);

КонецПроцедуры

Процедура ОбработатьОтвет(Ответ, КлючЗадачи)

	ТелоОтвета = Ответ.ПолучитьТелоКакСтроку();

	Чтение = Новый ЧтениеJSON();
	Чтение.УстановитьСтроку(ТелоОтвета);

	ДанныеОтвета = ПрочитатьJSON(Чтение);
	Чтение.Закрыть();

	Если Ответ.КодСостояния = 200 Тогда
		ВыполнитьПроверкуСостоянияЗадачи(ДанныеОтвета);
	Иначе
		Если Ответ.КодСостояния = 404 Тогда
			ВызватьИсключение "Задача с идентификатором " + КлючЗадачи + " не найдена";
		Иначе
			ВызватьИсключение ТелоОтвета;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Процедура ВыполнитьПроверкуСостоянияЗадачи(Задача)

	Если Задача.status.key <> "open" Тогда
		ВызватьИсключение "Фиксация изменений для задачи с состоянием """ + Задача.status.display + """ запрещена";
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьСоединение()

	Таймаут = 10;
	Возврат Новый HTTPСоединение(АдресАпи,,,,, Таймаут);

КонецФункции

Функция ПодготовитьЗапрос(Адрес, КлючЗадачи, Токен, Идентификатор)

	Заголовки = Новый Соответствие();
	Заголовки.Вставить("HOST", "api.tracker.yandex.net");
	Заголовки.Вставить("Authorization", "OAuth " + Токен);
	Заголовки.Вставить("X-Cloud-Org-ID", Идентификатор);
	Заголовки.Вставить("Accept-Language", "ru");

	Запрос = Новый HTTPЗапрос(Адрес + "/" + КлючЗадачи, Заголовки);

	Возврат Запрос;

КонецФункции

Функция ИзвлечьКлючЗадачиИзКомментария(Комментарий, РегулярноеВыражение)

	Выражение = Новый РегулярноеВыражение(РегулярноеВыражение);
	Выражение.ИгнорироватьРегистр = Истина;

	Совпадения = Выражение.НайтиСовпадения(Комментарий);

	Если Совпадения.Количество() <> 1 Тогда
		ВызватьИсключение "Не удалось извлечь ключ задачи из комментария";
	КонецЕсли;

	Возврат Совпадения[0].Значение;

КонецФункции

Функция ВыполнитьВалидациюПараметровСкрипта(Контекст)

	ТокенДоступа = Контекст.ДополнительныеПараметры.Получить("ТокенДоступа");
	ИдентификаторОрганизации = Контекст.ДополнительныеПараметры.Получить("ИдентификаторОрганизации");
	ВыражениеПоискаКлючаЗадачи = Контекст.ДополнительныеПараметры.Получить("ВыражениеПоискаКлючаЗадачи");
	
	Если ТокенДоступа = Неопределено Тогда
		ВызватьИсключение "Токен доступа к API яндекс трекера не указан";
	КонецЕсли;

	Если ИдентификаторОрганизации = Неопределено Тогда
		ВызватьИсключение "Идентификатор организации не указан";
	КонецЕсли;

	Если ВыражениеПоискаКлючаЗадачи = Неопределено Тогда
		ВызватьИсключение "Выражение поиска ключа задачи не указано";
	КонецЕсли;

	Параметры = Новый Структура();
	Параметры.Вставить("ТокенДоступа", ТокенДоступа);
	Параметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	Параметры.Вставить("ВыражениеПоискаКлючаЗадачи", ВыражениеПоискаКлючаЗадачи);

	Возврат Параметры;

КонецФункции

АдресАпи = "https://api.tracker.yandex.net";
АдресДеталейЗадач = "/v3/issues";